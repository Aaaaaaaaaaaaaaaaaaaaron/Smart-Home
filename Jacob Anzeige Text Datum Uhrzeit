//Platformio.ini

[env:az-delivery-devkit-v4]
platform = espressif32
board = az-delivery-devkit-v4
framework = arduino
lib_deps =
    knolleary/PubSubClient@^2.8
    adafruit/Adafruit ILI9341@^1.6.2
    adafruit/Adafruit GFX Library@^1.12.4
    adafruit/RTClib@^2.1.4


//main.cpp

#include <Wire.h>
#include <SPI.h>
#include <Adafruit_GFX.h>
#include <Adafruit_ILI9341.h>
#include <RTClib.h>
#include <WiFi.h>
#include <PubSubClient.h>

// Display Pins
#define TFT_CS   5
#define TFT_DC   2
#define TFT_RST  4

Adafruit_ILI9341 tft = Adafruit_ILI9341(TFT_CS, TFT_DC, TFT_RST);
RTC_DS3231 rtc;

// WiFi & MQTT
const char* ssid = "WLAN-RDF-EU";          // WLAN Name
const char* password = "wlan@RdF";  // WLAN Passwort
const char* mqttServer = "172.20.15.179";
const int mqttPort = 1883;
const char* mqttTopic = "Sensor/Test";

WiFiClient espClient;
PubSubClient client(espClient);

// Variablen für Texte
String greetingText = "Hallo Jacob";

// Sensorwerte als Zahlen
float temperature = 0;
float humidity = 0;
float pressure = 0;

// Funktion, um MQTT-Nachrichten zu verarbeiten
void callback(char* topic, byte* payload, unsigned int length) {
  String msg = "";
  for (unsigned int i = 0; i < length; i++) {
    msg += (char)payload[i];
  }
  // Format: Temp: 23.31 C, Hum: 39.40 %, Druck: 982.73 hPa
  int tIndex = msg.indexOf("Temp:");
  int hIndex = msg.indexOf("Hum:");
  int pIndex = msg.indexOf("Druck:");

  if (tIndex != -1 && hIndex != -1 && pIndex != -1) {
    String tempStr = msg.substring(tIndex + 5, hIndex - 1);
    String humStr = msg.substring(hIndex + 4, pIndex - 1);
    String presStr = msg.substring(pIndex + 6);

    temperature = tempStr.toFloat();
    humidity = humStr.toFloat();
    pressure = presStr.toFloat();
  }
}

// Verbindung zu MQTT herstellen
void reconnect() {
  while (!client.connected()) {
    Serial.print("Verbinde mit MQTT...");
    if (client.connect("ESP32Client")) {
      Serial.println("verbunden!");
      client.subscribe(mqttTopic);
    } else {
      Serial.print("Fehler, rc=");
      Serial.print(client.state());
      Serial.println(" Warte 5 Sekunden...");
      delay(5000);
    }
  }
}

void setup() {
  Serial.begin(115200);

  // RTC starten
  if (!rtc.begin()) {
    Serial.println("Keine RTC gefunden!");
    while (1);
  }

  if (rtc.lostPower()) {
    rtc.adjust(DateTime(F(__DATE__), F(__TIME__)));
  }

  // Display starten
  tft.begin();
  tft.setRotation(1);
  tft.fillScreen(ILI9341_BLACK);

  // WLAN verbinden
  WiFi.begin(ssid, password);
  Serial.print("Verbinde mit WLAN");
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }
  Serial.println(" verbunden!");

  client.setServer(mqttServer, mqttPort);
  client.setCallback(callback);
}

void loop() {
  if (!client.connected()) {
    reconnect();
  }
  client.loop();

  DateTime now = rtc.now();

  // Alles löschen, Bereich für Text und Werte
  tft.fillRect(0, 0, 320, 140, ILI9341_BLACK);

  // "Hallo Jacob" (Pink)
  tft.setCursor(10, 5);
  tft.setTextSize(2);
  tft.setTextColor(ILI9341_MAGENTA);
  tft.print(greetingText);

  // Sensorwerte anzeigen (Gelb)
  tft.setTextSize(2);
  tft.setTextColor(ILI9341_YELLOW);

  tft.setCursor(10, 30);
  tft.print("Temp: ");
  tft.print(temperature, 2);
  tft.print(" C");

  tft.setCursor(10, 50);
  tft.print("Humidity: ");
  tft.print(humidity, 2);
  tft.print("%");

  tft.setCursor(10, 70);
  tft.print("Pressure: ");
  tft.print(pressure, 2);
  tft.print(" hPa");

  // Datum rechts unten über Uhrzeit (Cyan)
  tft.setTextSize(2);
  tft.setTextColor(ILI9341_CYAN);

  String dateStr = "";
  if (now.day() < 10) dateStr += "0";
  dateStr += String(now.day()) + ".";
  if (now.month() < 10) dateStr += "0";
  dateStr += String(now.month()) + ".";
  dateStr += String(now.year());

  int dateX = tft.width() - 160;
  int dateY = tft.height() - 50;
  tft.setCursor(dateX, dateY);
  tft.print(dateStr);

  // Uhrzeit rechts unten (Rot)
  tft.setTextSize(3);
  tft.setTextColor(ILI9341_RED);

  int marginRight = 10;
  int marginBottom = 10;
  int textWidth = 8 * 8 * 3;
  int xPos = tft.width() - textWidth - marginRight;
  int yPos = tft.height() - 24 - marginBottom;

  tft.fillRect(xPos, yPos, textWidth, 24, ILI9341_BLACK);
  tft.setCursor(xPos, yPos);

  if (now.hour() < 10) tft.print("0");
  tft.print(now.hour());
  tft.print(":");
  if (now.minute() < 10) tft.print("0");
  tft.print(now.minute());
  tft.print(":");
  if (now.second() < 10) tft.print("0");
  tft.print(now.second());

  delay(1000);
}
